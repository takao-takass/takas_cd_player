/**************************************************************************/
/*   タカズＣＤプレーヤー ver 4.20                                        */
/*                                  企画開発：たかお                      */
/*                                  2011. 4.21 ベータ1 作成               */
/*                                  2011. 4.28 ベータ2 (Loop+)作成        */
/*                                  2011. 6.12 ベータ3 (Power Loop)作成   */
/*                                  2011.11.28 ver 1.00作成               */
/*                                  2011.12.20 ver 2.00作成               */
/*                                  2012. 1. 3 ver 3.00作成←中止         */
/*                                  2012. 3.17 ver 3.00作成               */
/*                                  2012. 8. 4 ver 3.10作成               */
/*                                  2012. 8.15 ver 3.20作成               */
/*                                  2012. 9.25 ver 3.30作成               */
/*                                  2012.12. 6 ver 3.40作成               */
/*                                  2013. 1.26 ver 3.41作成               */
/*                                  2013. 2.26 ver 3.42作成               */
/*                                  2013. 6. 3 ver 3.50作成               */
/*                                  2013. 8.30 ver 3.60作成               */
/*                                  2013. 9.30 ver 3.61作成               */
/*                                  2014. 1.28 ver 3.62作成               */
/*                                  2014. 3.17 ver 4.00作成               */
/*                                  2014. 8.15 ver 4.01作成               */
/*                                  2014.11.19 ver 4.10作成               */
/*                                  2015.12. 6 ver 4.20作成               */
/*                                                                        */
/*                                                                        */
/**************************************************************************/
	// 外部ソース（各種定義）の結合
	//#include "hspmix.as"			// 音量操作に利用
	#include "hspsock.as"			// プロトコル HTTPの通信に利用
	#include "system\\sub\\header.hsp"
	
	// システムデータの読み出し
	gosub *UpdateSystem	
	
	// システムの処理サイクル(数値が大きいほど処理を間引く)
	waitTime = 1
	
	// トラック情報を記録する配列
	dim trackNP,permissionTrack			// 再生禁止曲
	dim trackFV,permissionTrack			// お気に入り曲
	dim trackSP,permissionTrack			// ココカラ設定
	dim trackEP,permissionTrack			// ココマデ設定
//	dim trackAL,permissionTrack			// 追加ループ
	sdim saveTitleList,256,50			// トラック設定の保存画面で利用する
	
	// トラック音量を保持する配列
	dim trackVolume,permissionTrack		// トラック音量
	
	// シャッフル再生時に使用する配列
	dim shuffleArray,permissionTrack*4+1		// シャッフル再生時に使用する再生リスト
	dim trackListArray,permissionTrack+2		// 未選曲のトラック番号を格納する配列
	dim pickupCountArray,permissionTrack+1		// 対応するトラックの選曲回数を格納する配列
	
	// 設定画面で使用する文字列配列
	dim settingData,6					// 設定情報
	sdim settingTitleList,256,6			// 設定項目名
	settingTitle = ""					// 設定情報＋設定項目名
	settingTitleList(0) = "起動時に自動でバージョン確認を行う"
	settingTitleList(1) = "起動時に自動でCDの再生を開始する"
	settingTitleList(2) = "非アクティブ時はウインドウ名に再生情報を表示"
	settingTitleList(3) = "再生設定を自動保存して次回起動時まで保持する"
	settingTitleList(4) = "CDに応じて自動で再生設定を切り替える"
	settingTitleList(5) = "設定されている音量値を再生画面に表示する"
	settingTitleList(6) = "ボタンにカーソルを乗せたとき操作案内を表示する"
	
	// バージョン確認で用いる文字列配列
	sdim updateMessage,256,40
	
	// トラック情報を記録する配列の初期化
	ResetTrackArray trackNP,permissionTrack
	ResetTrackArray trackFV,permissionTrack
	ResetTrackArray trackSP,permissionTrack
	ResetTrackArray trackEP,permissionTrack
//	ResetTrackArray trackAL,permissionTrack
	
	// 設定データの読み出し
	gosub *LoadSettingData
	
	// ウインドウの設定
	title softName+" ver"+VersionIntToString(version)
	screen 0,sizeX,sizeY
	
	// リソースの先読み
	SystemArea = 4												// システムエリアの背景画像
	celload "system\\image\\SystemArea.img",SystemArea			// システムエリアの画像	
	
	// ウインドウ構築中の表示_2 (リソースを利用する)
	color 0,0,100
	pos 0,0 : celput 4
	//font "HGSｺﾞｼｯｸE",24
	ChangeFontSize 24
	pos 200,60 : print "起動中..."
	color 0,0,255
	//font "HGSｺﾞｼｯｸE",16
	ChangeFontSize 16
	pos 340,0 : print "ver "+VersionIntToString(version)

	// リソース番号の設定
	InfomationArea = 1			// インフォメーションエリアの背景画像
	TrackArea = 2				// トラックエリアの背景画像
	ControlArea = 3				// 操作エリアの背景画像
	tbutton = 5					// トラックボタンの画像
	PlayStateIcon = 6			// 再生状態アイコンの画像
	TrackAreaMoveIcon = 7		// トラックエリア移動アイコンの画像
	TrackKokoset = 8			// トラックのココ○○設定有無を示すアイコンの画像
	TrackSetting = 9			// トラックの設定を示すアイコンの画像
	PlayMenuIcon = 10			// 再生メニューのアイコン画像
	ButtonSmall = 11			// 再生メニュー用の小ボタン画像
	ControlMenuIcon = 12		// 操作メニューのアイコン画像
	ButtonNormal = 13			// 操作メニュー用のボタン画像
	PlayModeIcon = 14			// 再生モードのアイコン画像
	SeekTimeArea = 15			// シーク先時間の表示場所の画像
	ButtonLarge = 16			// 選択用ボタンの画像
	PageIcon = 17				// 読み出し・保存画面の上下三角アイコンの画像
	AddLoopIcon = 18			// 追加ループのアイコン
	ClickIcon = 19				// クリック案内のアイコン
	cdAudio = 20				// CDオーディオ（この番号からトラック分だけ使用する）

	// リソースの読み込み
	celload "system\\image\\InfomationArea.img",InfomationArea	// インフォメーションエリアの画像
	celload "system\\image\\TrackArea.img",TrackArea			// トラックエリアの画像 
	celload "system\\image\\ControlArea.img",ControlArea		// 操作エリアの画像
	celload "system\\image\\tbutton.img",tbutton				// トラックボタンの画像
	celload "system\\image\\PlayStateIcon.img",PlayStateIcon	// 再生状態アイコンの画像
	celload "system\\image\\TrackAreaMoveIcon.img",TrackAreaMoveIcon// トラックエリア移動アイコンの画像
	celload "system\\image\\TrackKokoset.img",TrackKokoset		// ココ○○設定の有無を示すアイコンの画像
	celload "system\\image\\TrackSetting.img",TrackSetting		// トラック設定の有無を示すアイコンの画像
	celload "system\\image\\PlayMenuIcon.img",PlayMenuIcon		// 再生メニューのアイコンの画像
	celload "system\\image\\ButtonSmall.img",ButtonSmall		// 再生メニュー用のボタン画像
	celload "system\\image\\ControlMenuIcon.img",ControlMenuIcon// 操作メニューのアイコンの画像
	celload "system\\image\\ButtonNormal.img",ButtonNormal		// 操作メニュー用のボタン画像
	celload "system\\image\\PlayModeIcon.img",PlayModeIcon		// 再生モードのアイコン画像
	celload "system\\image\\SeekTimeArea.img",SeekTimeArea		// シーク先時間の表示場所の画像
	celload "system\\image\\ButtonLarge.img",ButtonLarge		// 選択用ボタンの画像
	celload "system\\image\\AddLoopIcon.img",AddLoopIcon		// 追加ループのアイコン
	celload "system\\image\\PageIcon.img",PageIcon				// 読み出し・保存画面の上下三角アイコンの画像
	celload "system\\image\\ClickIcon.img",ClickIcon			// クリック案内のアイコン画像

	// 画像の分割
	celdiv tbutton,25,20			// トラックボタンの画像を分割
	celdiv PlayStateIcon,24,24		// 再生状態アイコンの画像を分割
	celdiv TrackAreaMoveIcon,30,24	// トラックエリア移動アイコンの画像を分割
	celdiv TrackKokoset,44,12		// ココ○○設定の有無を示すアイコンの画像を分割
	celdiv TrackSetting,28,24		// トラック設定の有無を示すアイコンの画像を分割
	celdiv PlayMenuIcon,17,12		// 再生メニューのアイコン画像を分割
	celdiv ButtonSmall,30,20		// 再生メニュー用の小ボタン画像を分割
	celdiv ControlMenuIcon,16,16	// 操作メニューのアイコン画像を分割
	celdiv ButtonNormal,30,25		// 操作メニュー用のボタン画像を分割
	celdiv PlayModeIcon,54,38		// 再生モードのアイコン画像を分割
	celdiv ButtonLarge,90,25		// 選択用ボタンの画像を分割
	celdiv AddLoopIcon,30,22		// 追加ループのアイコンを分割
	celdiv ClickIcon,18,18			// クリック案内のアイコンを分割
	celdiv PageIcon,22,22			// 読み出し・保存画面の上下三角アイコンの画像を分割
	
	// 既存バージョンの旧データを見つけたら引き継ぎ処理をするか確認する
	exist "C:\\takas\\twf001\\movedata.dat"
	if(strsize!=-1){
		// システムデータとトラックデータを引き継ぐ
		ConvertData
		// システムデータをもう一度読み出す
		gosub *UpdateSystem
		// 設定データをもう一度読み出す
		gosub *LoadSettingData
	}

	// システム状態のリセット
	gosub *ResetSystemStatus
	
	// 音量の取得
	//masterVolume = getMasterVolume()
	gosub *LoadVolumeData
	setMasterVolume masterVolume
	
	// 設定で自動確認を行わない場合は画面モードを『バージョン確認』→『CD挿入待機』に切り替える
	if(settingData(0)=0){
		//SM_VersionCheck = SM_CDInsertWait
		screenMode = SM_CDInsertWait
	}

*MainScreen
	gosub *KeyGet
	// 処理周期制御に依存しないシステム共通処理
	gosub *UpdateSystemArea_CommonProcess
	// 処理周期制御
	gosub *ProcessCycleControl
	waitCount--
	// 処理サイクルコントローラにより処理サイクルを制御
	if(waitCount<=0){
		waitCount = waitTime
		/****************************/
		/*      画面の描画更新      */
		/****************************/
		redraw 0
		/* システムエリアの描画更新 */
		switch screenMode
			case SM_CDInsertWait
				swbreak
			case SM_PlayMenu
			case SM_DeleteTrackSetting
			case SM_KokoSetting
			case SM_TrackDataSave
			case SM_SystemSetting
			case SM_TrackInfomation
			case SM_VolumeControl
			case SM_VolumeControlCDWait
			case SM_SystemSettingCDWait
				gosub *UpdateSystemArea_MainSystemControl
				swbreak
			case SM_VersionCheck
				gosub *UpdateSystemArea_CheckVersion
			swbreak
			default
				gosub *UpdateSystemArea_None
				swbreak
		swend
		/* インフォメーションエリアの描画更新 */
		switch screenMode
			case SM_CDInsertWait
				gosub *UpdateInfomationArea_Wait
				swbreak
			case SM_PlayMenu
				gosub *UpdateInfomationArea_PlayMenu
				swbreak
			case SM_DeleteTrackSetting
				gosub *UpdateInfomationArea_DeleteTrackSetting
				swbreak
			case SM_KokoSetting
				gosub *UpdateInfomationArea_KokoSetting
				swbreak
			case SM_SystemSetting
			case SM_SystemSettingCDWait
				gosub *UpdateInfomationArea_SystemSetting
				swbreak
			case SM_TrackDataSave
				gosub *UpdateInfomationArea_TrackDataSave
				swbreak
			case SM_TrackInfomation
				gosub *UpdateInfomationArea_TrackInfomation
				swbreak
			case SM_VolumeControl
				gosub *UpdateInfomationArea_VolumeControl
				swbreak
			case SM_VolumeControlCDWait
				gosub *UpdateInfomationArea_VolumeControlCDWait
				swbreak
			default
				swbreak
		swend
		/* トラックエリアの描画更新 */
		switch screenMode
			case SM_CDInsertWait
			case SM_VolumeControlCDWait
				gosub *UpdateTrackArea_Wait
				swbreak
			case SM_PlayMenu
			case SM_DeleteTrackSetting
			case SM_KokoSetting
			case SM_TrackInfomation
			case SM_VolumeControl
				gosub *UpdateTrackArea_PlayMenu
				swbreak
			default
				swbreak
		swend
		/* 操作エリアの描画更新 */
		switch screenMode
			case SM_CDInsertWait
			case SM_VolumeControlCDWait
				gosub *UpdateControlArea_Wait
				swbreak
			case SM_PlayMenu
			case SM_DeleteTrackSetting
			case SM_KokoSetting
			case SM_TrackInfomation
			case SM_VolumeControl
				gosub *UpdateControlArea_PlayMenu
				swbreak
			case SM_TrackDataSave
				gosub *UpdateControlArea_TrackDataSave
				swbreak
			case SM_SystemSetting
				gosub *UpdateControlArea_SystemSetting
				swbreak
			case SM_SystemSettingCDWait
				gosub *UpdateControlArea_SystemSettingCDWait
				swbreak
			default
				swbreak
		swend
		redraw 1
	}
		
	/* CDが未認識なら一定間隔でCDのチェックを実行する */
	if(allTrack = 0){
		if(screenMode!=SM_SystemSettingCDWait & screenMode!=SM_VolumeControlCDWait){
			if(cdCheckTimer = 0){
				if(CheckTrackCD(0) != -1){
					allTrack = CheckTrackCD(1)
					CdCode = CreateCDCode(allTrack)					// CDシグネチャコードの生成
					TrackListArraySet								// トラック一覧配列の構築
					RestoreAutoSaveData								// 自動保存データのリストア
					// CDシグネチャコードでトラック設定を自動読み込みする設定であれば実行する
					if(settingData(4)=1){
						RestoreSaveData(SearchDataByCdCode(CdCode))		// CDシグネチャコードで検出したデータをリストア
					}
					screenMode = SM_PlayMenu
					// 起動時に自動再生する設定であれば再生を開始する
					if(settingData(1)=1){
						PlayCD(1)
					}
				}else{
					if(alertByTrackOverFlag != 0){
						dialog "60トラックを超えるCDの再生には対応していません。",1,""
						alertByTrackOverFlag = 0
					}
				}
				cdCheckTimer = cdCheckCycle
			}
		cdCheckTimer-=1
		}
	}
	/* CDを認識していたら一定間隔でCDの取り出しをチェックする */
	if(allTrack != 0	/*screenMonde = SM_CDInsertWait*/){
		if(cdCheckTimer = 0){
			if(CheckTrackCD(0) = -1){
				StopCD
				gosub *ResetSystemStatus
				screenMode = SM_CDInsertWait
			}
			cdCheckTimer = cdCheckCycle/2
		}
		cdCheckTimer-=1
	}
	wait 2//waitTime
	goto *MainScreen
	
	// 外部ソース（各種処理プログラム）の結合
	#include "system\\sub\\system.hsp"
